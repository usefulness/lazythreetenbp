plugins {
    id("com.starter.library.android")
    id("com.starter.publishing")
    id("dev.zacsweers.ticktock")
}

Provider<Directory> generatedDir = layout.buildDirectory.dir("generated/ticktock")

tickTock {
    def tzdbVersion = libs.versions.maven.ticktock.get().split("-")[1]
    tzVersion.set(project.findProperty("tzdbVersion") ?: tzdbVersion)
    codeOutputDir.set(generatedDir)
    tzOutputDir.set(layout.projectDirectory.dir("src/main/assets"))
    packageName.set("com.gabrielittner.threetenbp")
}

android {
    compileSdk 31

    namespace = rootProject.name
    defaultConfig {
        minSdk 23

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    publishing {
        singleVariant("release")
    }
    lint {
        warningsAsErrors true
        checkReleaseBuilds false
    }
    sourceSets {
        main.java.srcDir(generatedDir)
    }
    androidComponents {
        beforeVariants(selector().withBuildType("release")) { builder ->
            builder.enable = true
        }
    }
}

dependencies {
    tickTockCompiler libs.ticktock.compiler
    compileOnly libs.annotation.core
    implementation "org.threeten:threetenbp:1.6.0:no-tzdb"

    androidTestImplementation libs.junit.core
    androidTestImplementation libs.truth.core
    androidTestImplementation libs.androidtest.runner
    androidTestImplementation libs.androidtest.junitext
}
